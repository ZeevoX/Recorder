import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

def buildDate() {
    def df = new SimpleDateFormat("yyyyMMdd")
    String formatted = df.format(new Date())
    return formatted
}

def gitVersionCode() {
    "git fetch".execute()
    def process = "git rev-list --all --count".execute()
    return process.text.toInteger()
}

def gitVersionName() {
    "git fetch".execute()
    def process = "git describe --tags --dirty=.debug".execute()
    return process.text.replaceAll('-', '\\.').trim()
}

android {

    signingConfigs {
        config {
            storeFile file("gitlab_keystore.jks")
            storePassword "recorder"
            keyAlias "Gitlab-PublicKey"
            keyPassword "recorder"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {

        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        applicationId "com.zeevox.recorder2"

        minSdkVersion 23
        targetSdkVersion 26

        versionCode gitVersionCode()
        versionName gitVersionName()
    }

    buildTypes {

        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
        }

        weekly {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.config

            versionNameSuffix '.weekly_' + buildDate()
        }

        beta {

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            versionNameSuffix '.beta'
        }

        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'

    /*               FABToolbar by fafaldo              */
    /*       https://github.com/fafaldo/FABToolbar      */
    //compile 'com.github.fafaldo:fab-toolbar:1.2.0'

    /*          Material Dialogs by afollestad          */
    /*  https://github.com/afollestad/material-dialogs  */
    //compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    //compile 'com.afollestad.material-dialogs:commons:0.9.4.5'

    /*               LicensesDialog by PSDev            */
    /*     https://github.com/PSDev/LicensesDialog      */
    compile('de.psdev.licensesdialog:licensesdialog:1.8.2')
}

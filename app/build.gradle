import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

/*
You may need to refresh tags from remote.
"git fetch --tags" does not delete local
tags. To delete all local tags and sync
from remote, type these commands in Bash.
[Courtesy of Richard W on Stack Overflow]
[  https://stackoverflow.com/a/5373319  ]
Delete all local tags regardless of type.
"git tag -l | xargs git tag -d"
Fetch all tags from remote as usual.
"git fetch --tags"
 */

def buildDate() {
    def df = new SimpleDateFormat("yyyyMMdd")
    return df.format(new Date())
}

def gitVersionCode() {
    "git fetch".execute()
    def process = "git rev-list --all --count".execute()
    return process.text.toInteger()
}

def gitSHALong() {
    "git fetch".execute()
    return "git rev-list --tags --max-count=1".execute().text.trim().toString()
}

def gitTag() {
    "git fetch".execute()
    String gitTag = "git describe --tags " + gitSHALong()
    return gitTag.execute().text.trim().toString()
}

def gitCommitsSinceTag() {
    "git fetch".execute()
    String command = "git rev-list " + gitSHALong() + "..HEAD --count"
    return command.execute().text.trim().toString()
}

def gitVersionName() {
    "git fetch".execute()
    return gitTag().toString() + "." + gitCommitsSinceTag().toString()
}

android {

    signingConfigs {
        old {
            storeFile file("gitlab_keystore.jks")
            storePassword "recorder"
            keyAlias "Gitlab-PublicKey"
            keyPassword "recorder"
        }
        config {
            storeFile file("../keystore.jks")
            storePassword System.getenv("STORE_PASSWORD").toString()
            keyAlias System.getenv("KEY_ALIAS").toString()
            keyPassword System.getenv("KEY_PASSWORD").toString()
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {

        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"

        applicationId "com.zeevox.recorder"

        minSdkVersion 23
        targetSdkVersion 26

        versionCode gitVersionCode()
        versionName gitVersionName()

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                if ((variant.name) != "travis") {
                    outputFileName = "Recorder-${versionCode}-${versionName}.apk"
                }
            }
        }
    }

    buildTypes {

        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            versionNameSuffix '.debug_' + buildDate()
        }

        weekly {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.config
            versionNameSuffix '.weekly_' + buildDate()
        }

        beta {

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            versionNameSuffix '.beta'
        }

        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            versionNameSuffix '.release'
        }

        travis {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.config
            versionNameSuffix '.weekly_' + buildDate()
        }
    }
}

dependencies {
    //implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.0.0-beta2'
    implementation 'com.android.support:design:26.0.0-beta2'
    //implementation 'com.android.support:recyclerview-v7:26.0.0-beta2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    //implementation 'com.android.support:preference-v7:26.0.0-beta2'

    /*          Material Dialogs by afollestad          */
    /*  https://github.com/afollestad/material-dialogs  */
    implementation 'com.afollestad.material-dialogs:core:0.9.4.5'
    implementation 'com.afollestad.material-dialogs:commons:0.9.4.5'

    /*               LicensesDialog by PSDev            */
    /*     https://github.com/PSDev/LicensesDialog      */
    //implementation 'de.psdev.licensesdialog:licensesdialog:1.8.2'

    /*          Android Audio Library by axet           */
    /*   https://gitlab.com/axet/android-audio-library  */
    //implementation 'com.github.axet:android-audio-library:1.0.28'
}
